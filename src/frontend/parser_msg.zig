pub const ParserMsg = union(enum) {
    ChainingCmpOp,
    ExpectArrowBeforeFnType,
    ExpectBraceBeforeFnBody,
    ExpectBraceAfterStructBody,
    ExpectBraceAfterStructLit,
    ExpectBraceBeforeStructBody,
    ExpectBraceOrDo: struct { what: []const u8 },
    ExpectcolonBeforeType,
    ExpectExpr: struct { found: []const u8 },
    ExpectFieldName,
    ExpectFnName,
    ExpectFnInStructBody,
    ExpectName: struct { kind: []const u8 },
    ExpectNameAfterDot,
    ExpectNewLine,
    ExpectParenAfterFnArgs,
    ExpectParenAfterFnName,
    ExpectParenAfterFnParams,
    ExpectStructName,
    ExpectTypeName,
    ExpectFieldTypeOrDefault,
    InvalidDiscard,
    MissingCommaAfterField,
    MissingFnParamType,
    SelfAsNonFirstParam,
    StructLitNonIdentField,
    TooManyFnArgs: struct { what: []const u8 },
    TypedSelf,
    UnclosedBrace,
    UnclosedParen,
    WrongValueCountVarDecl: struct { expect: usize },

    const Self = @This();

    pub fn get_msg(self: Self, writer: anytype) !void {
        try switch (self) {
            .ChainingCmpOp => writer.print("chaining comparison operators", .{}),
            .ExpectArrowBeforeFnType => writer.print("expect arrow '->' before function type", .{}),
            .ExpectBraceOrDo => |e| writer.print("expect opening brace or 'do' after '{s}' condition", .{e.what}),
            .ExpectBraceBeforeFnBody => writer.print("expect opening brace before function's body", .{}),
            .ExpectBraceAfterStructBody => writer.print("expect opening brace after structure's body", .{}),
            .ExpectBraceAfterStructLit => writer.print("expect opening brace after structure's name in structure initialization", .{}),
            .ExpectBraceBeforeStructBody => writer.print("expect opening brace before structure's body", .{}),
            .ExpectcolonBeforeType => writer.print("invalid variable type declaration", .{}),
            .ExpectExpr => |e| writer.print("expected expression, found \"{s}\"", .{e.found}),
            .ExpectFieldName => writer.writeAll("expect structure field name"),
            .ExpectFnName => writer.print("expect function name after 'fn' keyword", .{}),
            .ExpectFnInStructBody => writer.writeAll("expect functions declaration or nothing after structure's fields"),
            .ExpectName => |e| writer.print("expect {s} name ", .{e.kind}),
            .ExpectNameAfterDot => writer.writeAll("expect field name after dot"),
            .ExpectNewLine => writer.print("expect new line after statement", .{}),
            .ExpectParenAfterFnArgs => writer.print("expect closing parenthesis ')' after function's arguments", .{}),
            .ExpectParenAfterFnName => writer.print("expect opening parenthesis '(' after function's name", .{}),
            .ExpectParenAfterFnParams => writer.print("expect closing parenthesis ')' after function's parameters", .{}),
            .ExpectStructName => writer.print("expect structure name", .{}),
            .ExpectTypeName => writer.print("expect type name", .{}),
            .ExpectFieldTypeOrDefault => writer.writeAll("structure fileds must be typed or have a default value"),
            .InvalidDiscard => writer.print("invalid discard expression", .{}),
            .MissingCommaAfterField => writer.writeAll("comma might be missing between fields"),
            .MissingFnParamType => writer.print("missing function's parameter's type", .{}),
            .SelfAsNonFirstParam => writer.writeAll("'self' parameter not in first position"),
            .StructLitNonIdentField => writer.writeAll("trying to assign to a non-field in structure literal"),
            .TooManyFnArgs => |e| writer.print("functions can't have more than 255 {s}", .{e.what}),
            .TypedSelf => writer.writeAll("can't specify a type for 'self', it's a keyword whose type is known by the compiler"),
            .UnclosedBrace => writer.print("unclosed brace", .{}),
            .UnclosedParen => writer.print("unclosed parenthesis", .{}),
            .WrongValueCountVarDecl => |e| writer.print(
                "value count mismatch variable count, expect {} values",
                .{e.expect},
            ),
        };
    }

    pub fn get_hint(self: Self, writer: anytype) !void {
        try switch (self) {
            .ChainingCmpOp => writer.print("this one is not allowed", .{}),
            .ExpectArrowBeforeFnType,
            .ExpectBraceBeforeFnBody,
            .ExpectBraceAfterStructBody,
            .ExpectBraceAfterStructLit,
            .ExpectBraceBeforeStructBody,
            .ExpectBraceOrDo,
            .ExpectNameAfterDot,
            .ExpectParenAfterFnArgs,
            .ExpectParenAfterFnName,
            .ExpectParenAfterFnParams,
            .ExpectStructName,
            .MissingCommaAfterField,
            .MissingFnParamType,
            => writer.print("expect to be here", .{}),
            .ExpectcolonBeforeType => writer.print("before this identifier", .{}),
            .ExpectFieldName => writer.writeAll("this is not an identifier"),
            .ExpectFnInStructBody => writer.writeAll("this is unexpected"),
            .ExpectExpr, .ExpectNewLine => writer.print("here", .{}),
            .ExpectFnName, .ExpectTypeName, .ExpectName => writer.print("this is not an identifier", .{}),
            .ExpectFieldTypeOrDefault => writer.writeAll("this field has no type and not default value"),
            .InvalidDiscard => writer.print("expect an assignment to '_'", .{}),
            .SelfAsNonFirstParam => writer.writeAll("here"),
            .StructLitNonIdentField => writer.writeAll("this field"),
            .TypedSelf => writer.writeAll("this type"),
            .TooManyFnArgs => |e| writer.print("this is the 256th {s}", .{e.what}),
            .UnclosedBrace => writer.print("this opening brace", .{}),
            .UnclosedParen => writer.print("this opening parenthesis", .{}),
            .WrongValueCountVarDecl => writer.writeAll("from this variable"),
        };
    }

    pub fn get_help(self: Self, writer: anytype) !void {
        try switch (self) {
            .ChainingCmpOp => writer.print("split your comparison with 'and' and 'or' operators", .{}),
            .ExpectArrowBeforeFnType => writer.print("add an arrow '->' between function's arguments list and type", .{}),
            .ExpectBraceBeforeFnBody, .ExpectBraceBeforeStructBody => writer.print("add an opening brace '{{'", .{}),
            .ExpectBraceAfterStructBody => writer.writeAll("add an closing brace '}}'"),
            .ExpectBraceAfterStructLit => writer.writeAll("add an openning brace '{{'"),
            .ExpectBraceOrDo => writer.print("add an opening brace '{{' or 'do' keyword before statement", .{}),
            .ExpectParenAfterFnArgs => writer.print("add an closing parenthesis ')' after function call", .{}),
            .ExpectParenAfterFnName => writer.print(
                "add an opening parenthesis '(' between function's name and arguments list",
                .{},
            ),
            .ExpectParenAfterFnParams => writer.print(
                "add an closing parenthesis ')' between function's parameters and return type",
                .{},
            ),
            .ExpectcolonBeforeType => writer.print("add ':' bofre type name", .{}),
            .ExpectFieldName => writer.writeAll("structures fields must be declared before constants and methods"),
            .ExpectFnInStructBody => writer.writeAll("structures must declare their fields first followed by their methods, nothing more"),
            .ExpectFnName, .ExpectTypeName, .ExpectName, .ExpectStructName => writer.print("define an identifier", .{}),
            .ExpectNameAfterDot => writer.writeAll("field access syntax is: 'Structure.field'"),
            .ExpectFieldTypeOrDefault => writer.writeAll("add a type to the field like: 'field: type' or add a default value like: 'field = value'"),
            .InvalidDiscard => writer.print("add '=' token: _ = call()", .{}),
            .MissingCommaAfterField => writer.writeAll("structure fields have ot be separated by commas"),
            .MissingFnParamType => writer.print("function's paramters' types are mandatory, add it after ':'", .{}),
            .SelfAsNonFirstParam => writer.writeAll("'self' parameter must always be in first place. Change function's declaration"),
            .StructLitNonIdentField => writer.writeAll("structure literal must define a value for all fields that doesn't have a default one"),
            .TooManyFnArgs => |e| writer.print(
                "split your function into multiple small ones or pass your {s} in structures / arrays",
                .{e.what},
            ),
            .TypedSelf => writer.writeAll("remove the type, the compiler infers the type for 'self' on its own"),
            .UnclosedBrace => writer.print("close the opening brace", .{}),
            .UnclosedParen => writer.print("close the opening parenthesis", .{}),
            .WrongValueCountVarDecl => writer.writeAll(
                "you must either provide no value, 1 value that will assigned to each variable or one per variable",
            ),
            else => {},
        };
    }
};
